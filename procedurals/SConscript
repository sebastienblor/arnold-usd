# vim: filetype=python

## load our own python modules
import system
from build_tools import find_files_recursive

import os, glob

# import build env
Import('env')
local_env = env.Clone(SHLIBPREFIX = '')

# Add new procedural directory names here
PROCEDURALS = ['ParticleInstancer', 'xgen'] 

source_files = []
include_files = []

MTOA_PROCS = []

local_env.Append(CPPPATH = ['.'])
local_env.Append(LIBS = Split('ai'))

# For every procedural found, get a list of source and include files
for dir in PROCEDURALS:
    base_dir = os.path.join(local_env['ROOT_DIR'], 'procedurals', dir)
    if os.path.isdir(base_dir) and dir != '.svn':
        sconscriptPath = os.path.join(base_dir, "SConscript")
        if os.path.exists(sconscriptPath):
            proc_env = local_env.Clone()
            MTOA_PROCS    += proc_env.SConscript(sconscriptPath,
                             duplicate   = 0,
                             variant_dir = os.path.join(env['BUILD_BASE_DIR'], 'procedurals', dir),
                             exports     = 'proc_env')
        else:
            proc_src_files = [os.path.join(dir, name) for name in find_files_recursive(base_dir, ['.c', '.cpp'])]
            proc_inc_files = [os.path.join(dir, name) for name in find_files_recursive(base_dir, ['.h'])]
            source_files  += proc_src_files
            include_files += proc_inc_files
            # Name of the final dll will be "mtoa_<directory>_proc"
            MTOA_PROCS    += local_env.SharedLibrary(os.path.join(dir, 'mtoa_%s_proc' % dir), proc_src_files)

if system.os() == 'darwin':
    local_env.Append(CCFLAGS = Split('-fvisibility=hidden')) # hide symbols by default
    #local_env.Append(LINKFLAGS = Split('-Wl,-rpath="\\$$ORIGIN" -z origin'))
elif system.os() != 'windows':
    local_env.Append(CCFLAGS = Split('-fvisibility=hidden')) # hide symbols by default
    local_env.Append(LINKFLAGS = Split('-Wl,-rpath="\\$$ORIGIN" -z origin'))

Return('MTOA_PROCS')
