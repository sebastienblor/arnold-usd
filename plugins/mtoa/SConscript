# vim: filetype=python

## load our own python modules
import system
from build_tools import find_files_recursive

import os

# import build env
Import('env')
local_env = env.Clone()

# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(local_env['ROOT_DIR'], 'plugins', 'mtoa')
source_files  = find_files_recursive(src_base_dir, ['.c', '.cpp'])
include_files = find_files_recursive(src_base_dir, ['.h'])

if system.os() =='windows':
   local_env.Append(CPPPATH = ['.'])
   local_env.Append(CPPPATH = [os.path.join(env['MAYA_ROOT'], 'include')])
   local_env.Append(CPPDEFINES = Split('NT_PLUGIN REQUIRE_IOSTREAM'))
   local_env.Append(LIBPATH = [os.path.join(env['MAYA_ROOT'], 'lib')])
   local_env.Append(LIBS=Split('ai.lib OpenGl32.lib glu32.lib Foundation.lib OpenMaya.lib OpenMayaRender.lib OpenMayaUI.lib OpenMayaAnim.lib OpenMayaFX.lib'))
else:
   local_env.Append(CPPPATH = ['.'])
   local_env.Append(CPPPATH = [os.path.join(env['MAYA_ROOT'], 'include')])
   local_env.Append(CPPDEFINES = Split('LINUX _BOOL REQUIRE_IOSTREAM'))
   local_env.Append(LIBPATH = [os.path.join(env['MAYA_ROOT'], 'lib')])
   local_env.Append(LIBS=Split('ai GL GLU Foundation OpenMaya OpenMayaRender OpenMayaUI OpenMayaAnim OpenMayaFX'))

MTOA = local_env.SharedLibrary('mtoa', source_files)

if system.os() == 'windows':
   MTOA_PRJ = local_env.MSVSProject(target = 'mtoa' + env['MSVS']['PROJECTSUFFIX'],
                                    srcs = source_files + include_files,
                                    incs = [],
                                    buildtarget = 'mtoa',
                                    cmdargs = ['-Q -s COMPILER=msvc MODE=debug TARGET_ARCH=x86',
                                               '-Q -s COMPILER=icc MODE=debug TARGET_ARCH=x86',
                                               '-Q -s COMPILER=msvc MODE=opt TARGET_ARCH=x86',
                                               '-Q -s COMPILER=icc MODE=opt TARGET_ARCH=x86'],
                                    variant = ['Debug_MSVC|Win32',
                                               'Debug_ICC|Win32',
                                               'Opt_MSVC|Win32',
                                               'Opt_ICC|Win32'],
                                    auto_build_solution = 0,
                                    nokeep = 1)

   local_env.AlwaysBuild(MTOA_PRJ)
   
   Return('MTOA MTOA_PRJ')
else:
   Return('MTOA')
