# vim: filetype=python

## load our own python modules
import system
from build_tools import find_files_recursive

import os

# import build env
Import('env')
local_env = env.Clone()

# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(local_env['ROOT_DIR'], 'shaders', 'src')
source_files  = find_files_recursive(src_base_dir, ['.c', '.cpp'])
include_files = find_files_recursive(src_base_dir, ['.h'])

if system.os() =='windows':
   local_env.Append(CPPPATH = ['.'])
   local_env.Append(LIBS=Split('ai.lib'))
else:
   local_env.Append(CPPPATH = ['.'])
   local_env.Append(LIBS=Split('ai'))
   local_env.Append(CCFLAGS = Split('-fvisibility=hidden')) # hide symbols by default


MTOA_SHADERS = local_env.SharedLibrary('mtoa_shaders', source_files, LIBPREFIX='')

if system.os() == 'windows':
   local_env['MSVSSCONSCRIPT'] = local_env.File('SConscript')
   MTOA_SHADERS_PRJ = local_env.MSVSProject(target = 'mtoa_shaders' + env['MSVS']['PROJECTSUFFIX'],
                                            srcs = source_files + include_files,
                                            incs = [],
                                            buildtarget = 'shaders',
                                            cmdargs = ['-Q -s COMPILER=msvc MODE=debug TARGET_ARCH=x86',
                                                       '-Q -s COMPILER=icc MODE=debug TARGET_ARCH=x86',
                                                       '-Q -s COMPILER=msvc MODE=opt TARGET_ARCH=x86',
                                                       '-Q -s COMPILER=icc MODE=opt TARGET_ARCH=x86'],
                                            variant = ['Debug_MSVC|Win32',
                                                       'Debug_ICC|Win32',
                                                       'Opt_MSVC|Win32',
                                                       'Opt_ICC|Win32'],
                                            auto_build_solution = 0,
                                            nokeep = 1)
   
   local_env.AlwaysBuild(MTOA_SHADERS_PRJ)
   
   Return('MTOA_SHADERS MTOA_SHADERS_PRJ')
else:
   Return('MTOA_SHADERS')
