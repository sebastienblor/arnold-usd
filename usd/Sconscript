import os, re
import platform
import sys
import SCons.Action
import subprocess

import utils as sa

# vim: filetype=python

## load our own python modules
import utils.system as system
from utils.build_tools import find_files_recursive


# import build env
Import('env')
local_env = env.Clone()
__target = local_env.Dir('.')
globals()['USD_DELEGATE'] = []


# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(local_env['ROOT_DIR'], 'external', 'arnold-usd')
source_files  = find_files_recursive(src_base_dir, ['.c', '.cpp'])
include_files = find_files_recursive(src_base_dir, ['.h'])

mayaRoot = env['MAYA_ROOT']
'''
if system.os == 'darwin':
    mayaRoot = mayaRoot.replace('Maya.app/Contents', '')
    mayaRoot = mayaRoot.replace('//', '/')
'''
local_env.Append(CPPPATH = ['.'])

lib_extension = {'darwin' : '.dylib' , 'linux'  : '.so' , 'windows': '.dll'}.get(platform.system().lower(), None)
lib_prefix = {'windows' : ''}.get(system.os, '')
lib_suffix = {'darwin' : '.dylib', 'linux'  : '.so', 'windows': '.dll'}.get(system.os)

usd_install_dir = env['USD_PATH']
usd_lib_dir = os.path.join(usd_install_dir, 'lib')

usd_supported_modes = ['opt', 'debug', 'profile']

usd_include_dir = os.path.join(usd_install_dir, 'include')
boost_lib = os.path.join(usd_install_dir, 'lib')
if platform.system().lower() == 'darwin':
    boost_include = os.path.join(usd_install_dir, 'include', 'boost')
    boost_lib_name = 'boost_%s37'
    python_include = os.path.join(mayaRoot, 'Frameworks', 'Python.framework', 'Versions', '3.7', 'include', 'python3.7m')
    python_lib = os.path.join(mayaRoot, 'Frameworks', 'Python.framework', 'Versions', '3.7', 'lib')
    usd_lib_prefix = 'lib'
else:
    boost_include = os.path.join(usd_install_dir, 'include', 'boost-1_70')
    boost_lib_name = 'boost_%s37-vc141-mt-x64-1_70'
    python_include = os.path.join(env['MAYA_INCLUDE_PATH'], 'Python37', 'Python')
    python_lib = os.path.join(mayaRoot, 'lib')
    usd_lib_prefix = '""'

usd_delegate_folder = os.path.join(__target.abspath, 'hydra', env['USD_VERSION'])
if not os.path.exists(usd_delegate_folder):
    os.makedirs(usd_delegate_folder) 

if not os.path.exists(python_include):
    print("Python includes not found %s"% python_include)
if not os.path.exists(python_lib):
    print("Python libs not found %s"% python_lib)
if not os.path.exists(boost_include):
    print("Boost includes not found %s"% boost_include)
if not os.path.exists(usd_include_dir):
    print("USD includes not found %s"% usd_include_dir)
    

usd_delegate = os.path.join(usd_delegate_folder, "hdArnold"+lib_extension)

def _usd_delegate(target, source, env):
    usd_env = env.Clone()
    #########
    args = dict(
      COMPILER = 'msvc' if system.is_windows else env['COMPILER'],
      ARNOLD_PATH         = env['ARNOLD'],
      USD_PATH= usd_install_dir,
      USD_BIN= usd_lib_dir,
      TBB_LIB= usd_lib_dir,
      TBB_STATIC=True,
      USD_BUILD_MODE = 'shared_libs',
      PREFIX_RENDER_DELEGATE= usd_delegate_folder,
      BUILD_SCHEMAS=False,
      BUILD_RENDER_DELEGATE=True,
      BUILD_USD_WRITER=False,
      BUILD_PROCEDURAL=False,
      BUILD_TESTSUITE=False,
      BUILD_DOCS=False,
      BOOST_LIB= boost_lib,
      BUILD_HOUDINI_TOOLS=False,
      BUILD_NDR_PLUGIN=False,
      MODE= env['MODE'],
      WARN_LEVEL='warn-only',
      MSVC_VERSION='14.1',
      CXX_STANDARD='14',
      BOOST_INCLUDE=boost_include,
      BOOST_LIB_NAME=boost_lib_name,
      PYTHON_INCLUDE=python_include,
      PYTHON_LIB=python_lib,
      USD_LIB_PREFIX=usd_lib_prefix
    )
    if platform.system().lower() == 'darwin':
        args['SDK_PATH']          = env['SDK_PATH']
        args['SDK_VERSION']       = env['SDK_VERSION']
        args['MACOS_VERSION_MIN'] = env['MACOS_VERSION_MIN']
        args['PYTHON_LIB_NAME']   = 'libpython3.7'
      
      
    usd_project = env.Dir(os.path.join(env['EXTERNAL_PATH'], 'arnold-usd')).abspath
    if not os.path.exists(usd_project):
        raise SCons.Errors.UserError('arnold-usd subrepository not found')

    usd_scons = os.path.join(usd_project, 'tools', 'scons', 'scons.py')
    if not os.path.exists(usd_scons):
        raise SCons.Errors.UserError('arnold-usd scons file not found')

    usd_scons_site = os.path.join(usd_project, 'tools', 'scons-custom')
    args = ['-B', usd_scons, '--site-dir={}'.format(usd_scons_site), 'delegate-install', '-j', '{}'.format(env.GetOption('num_jobs'))] + ['{}={}'.format(k, v) for k, v in args.items()]
   
    sub = subprocess.Popen([sys.executable] + args, cwd=usd_project)
    sub.communicate()
    if sub.returncode != 0:
        raise SCons.Errors.UserError('Error building arnold-usd subrepository!')
    return None

usd_delegate_action  = SCons.Action.Action(_usd_delegate    , 'Building USD render delegate ...')
USD_DELEGATE = local_env.Command(usd_delegate_folder, [], usd_delegate_action  )
local_env.Alias('usd_delegate', USD_DELEGATE)

if not env.get('MAYAUSD_PATH'):
    Return('USD_DELEGATE')

##########################################################
###### Now Maya USD registry 
mayausd_env = env.Clone()

src_base_dir  = os.path.join(mayausd_env['ROOT_DIR'], 'usd')
include_files =  ['mayaUsdRegistry.h', 'usdArnoldShaderReader.h',  'usdArnoldShaderWriter.h']
source_files  = ['mayaUsdRegistry.cpp', 'usdArnoldShaderReader.cpp', 'usdArnoldShaderWriter.cpp']

mayausd_include = os.path.join(env['MAYAUSD_PATH'], 'include')
mayausd_lib = os.path.join(env['MAYAUSD_PATH'], 'lib')
mayausd_env.Append(CPPDEFINES = Split('_BOOL'))
maya_version_base = mayausd_env['MAYA_VERSION_BASE']
mayausd_env.Append(CPPPATH = ['.'])
mayausd_env.Append(CPPPATH = [usd_include_dir, boost_include, python_include, mayausd_include])
mayausd_env.Append(CPPPATH = [env['MAYA_INCLUDE_PATH']])
mayausd_env.Append(LIBPATH = [usd_lib_dir, python_lib, boost_lib, mayausd_lib])
mayausd_env['SHOW_CMDS']=True
mayausd_env['BOOST_LIB_NAME']=boost_lib_name

usd_libs = [
        'arch',
        'sdf',
        'tf',
        'usd',
        'ar',
        'usdGeom',
        'usdShade',
        'vt',
        'usdLux',
        'usdVol',
        'usdSkel',
        'usdRender',
    ]

mayausd_env.Append(CPPPATH = ['.'])
mayausd_env.Append(LIBS = usd_libs)
mayausd_env.Append(LIBS = ['mayaUsd'])
if platform.system().lower() == 'darwin':
    mayausd_env['PYTHON_LIB_NAME'] = 'libpython3.7'
    mayausd_env.Append(LIBS = ['python3.7', boost_lib_name])
    mayausd_env.Append(LIBPATH = [os.path.join(mayaRoot, 'MacOS')])

if system.os =='windows':
    mayausd_env.Append(LIBS=Split('ai.lib OpenGl32.lib Foundation.lib OpenMaya.lib OpenMayaRender.lib OpenMayaUI.lib OpenMayaAnim.lib OpenMayaFX.lib shell32.lib'))
else:
    mayausd_env.Append(LIBS=Split('ai pthread Foundation OpenMaya OpenMayaRender OpenMayaUI OpenMayaAnim OpenMayaFX'))

MAYAUSD_REGISTRY = mayausd_env.SharedLibrary('mayaUsdRegistry', source_files, LIBPREFIX='')


Return('USD_DELEGATE MAYAUSD_REGISTRY')