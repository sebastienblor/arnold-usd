# vim: filetype=python

import sys, os
import SCons.Action
import subprocess

# import build env
Import('maya_env')
local_env = maya_env.Clone()
_targetPath = local_env.Dir('.')
globals()['MTOA_ARV'] = []

# let's get the base/root directory first
src_base_dir  = os.path.join(local_env['ROOT_DIR'], 'external', 'arnold-renderview')
# extend the module path to load our own modules
py_tools_dir = os.path.join(src_base_dir,
                            'tools',
                            'python')
if py_tools_dir not in sys.path:
    sys.path = [py_tools_dir] + sys.path
# load our own python modules
import system
from utils.build_tools import find_files_recursive

# source and include files
icons_dir  = os.path.join(local_env['ROOT_DIR'], 'icons')
source_files = [os.path.join(src_base_dir, f) for f in find_files_recursive(src_base_dir, ['.c', '.cpp']) if not f.startswith('glcontext_guard')]
include_files = [f for f in find_files_recursive(src_base_dir, ['.h']) if not f.startswith('glcontext_guard')]
resource_files = [f for f in find_files_recursive(icons_dir, ['.qrc'])]

mayaVersion = int(local_env['MAYA_VERSION_BASE'])

if system.os() == 'darwin':
    source_files += Split('glcontext_guard_mac.mm')
    lib_ext = '.dylib'
else:
    source_files += Split(os.path.join(src_base_dir, 'glcontext_guard.cpp'))
    if system.os() == 'windows':
        lib_ext = '.dll'
    else: # linux
        lib_ext = '.so'

mtoa_arv_folder = _targetPath.abspath

def _mtoa_arv(target, source, env):
    arv_env = env.Clone()
    args = dict(
        ARNOLD = local_env['ARNOLD'],
        SHOW_CMDS = local_env['SHOW_CMDS'],
        CCFLAGS = local_env['CCFLAGS'],
        MSVC_VERSION = '14.2',
        TARGET_ARCH = local_env['TARGET_ARCH'],
        MODE = local_env['MODE'],
        RV_OCIO = True,
        RV_SYNCOLOR = False,
        QT_LIB_PATH = os.path.join(local_env['MAYA_ROOT'], 'lib'),
        QT_INCLUDE_PATH = local_env['QT_ROOT_DIR'],
        INSTALL_DIR = mtoa_arv_folder,
    )
    if system.os() == 'linux':
        if local_env.get('SHCXX'):
            args['SHCXX'] = local_env['SHCXX']
        if local_env.get('SHCC'):
            args['SHCC'] = local_env['SHCC']
    elif system.os() == 'darwin':
        args['SDK_VERSION'] = local_env.get('SDK_VERSION', '')
    arv_project = env.Dir(os.path.join(env['ROOT_DIR'], env['EXTERNAL_PATH'], 'arnold-renderview')).abspath
    arv_scons = os.path.join(arv_project, '3rdparty', 'scons', 'scons.py')
    args = ['-B', arv_scons, 'install', '-j', '1'] + ['{}={}'.format(k, v) for k, v in args.items()]
    sub = subprocess.Popen([sys.executable] + args, cwd=arv_project)
    sub.communicate()
    if sub.returncode != 0:
        raise SCons.Errors.UserError('Error building arnold-renderview subrepository!')
    return None

mtoa_arv_action_log = 'Building MTOA ARV ...'
mtoa_arv_action  = SCons.Action.Action(_mtoa_arv, mtoa_arv_action_log)
if system.os() == 'windows':
    MTOA_ARV = local_env.Command(os.path.join(mtoa_arv_folder, 'ai_renderview%s' % lib_ext), [], mtoa_arv_action)
else:
    MTOA_ARV = local_env.Command(os.path.join(mtoa_arv_folder, 'libai_renderview%s' % lib_ext), [], mtoa_arv_action)
local_env.Alias('mtoa_arv', MTOA_ARV)

Return('MTOA_ARV')
