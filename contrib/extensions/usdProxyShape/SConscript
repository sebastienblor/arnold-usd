# vim: filetype=python

import utils.system as system
import os
import platform
from utils.build_tools import find_files_recursive
from utils.mtoa_build_tools import get_library_extension

Import('ext_env')

local_env = ext_env.Clone()

BUILD_BASE_DIR = local_env['BUILD_BASE_DIR']
ROOT_DIR = local_env['ROOT_DIR']

mayaRoot = local_env['MAYA_ROOT']
if system.os == 'darwin':
    mayaRoot = mayaRoot.replace('Maya.app/Contents', '')
    mayaRoot = mayaRoot.replace('//', '/')

if system.os !='windows':
    local_env.Append(CCFLAGS = Split('-fvisibility=hidden')) # hide symbols by default

# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(ROOT_DIR, 'contrib', 'extensions', 'usdProxyShape', 'plugin')
source_files  = [os.path.join('plugin', x) for x in find_files_recursive(src_base_dir, ['.c', '.cpp'])]

#only to be done if USD_PATH is defined
if local_env.get('USD_PATH'):
    usd_install_dir = local_env['USD_PATH']
    usd_lib_dir = os.path.join(usd_install_dir, 'lib')
    usd_include_dir = os.path.join(usd_install_dir, 'include')

    boost_lib = os.path.join(usd_install_dir, 'lib')
    if platform.system().lower() == 'darwin':
        boost_include = os.path.join(usd_install_dir, 'include', 'boost')
        boost_lib_name = 'boost_%s37'
        python_include = os.path.join(local_env['MAYA_ROOT'], 'Frameworks', 'Python.framework', 'Versions', '3.7', 'include', 'python3.7m')
        python_lib = os.path.join(local_env['MAYA_ROOT'], 'Frameworks', 'Python.framework', 'Versions', '3.7', 'lib')
        usd_lib_prefix = 'lib'
    else:
        boost_include = os.path.join(usd_install_dir, 'include', 'boost-1_70')
        boost_lib_name = 'boost_%s37-vc141-mt-x64-1_70'
        python_include = os.path.join(local_env['MAYA_INCLUDE_PATH'], 'Python37', 'Python')
        python_lib = os.path.join(local_env['MAYA_ROOT'], 'lib')
        usd_lib_prefix = '""'

    usd_libs = [
        'arch',
        'sdf',
        'tf',
        'usd',
        'ar',
        'usdGeom',
        'usdShade',
        'usdUtils',
        'vt',
        'usdLux',
        'usdVol',
        'usdSkel',
        'usdRender',
    ]

    if platform.system().lower() == 'darwin':
        usd_libs = ['%s%s' % (usd_lib_prefix, usd_lib) for usd_lib in usd_libs]

    local_env.Append(LIBS = usd_libs)

    local_env.Append(CPPPATH = [usd_include_dir, boost_include, python_include])
    local_env.Append(LIBPATH = [usd_lib_dir, python_lib, boost_lib])

    if platform.system().lower() == 'darwin':
        local_env['PYTHON_LIB_NAME'] = 'libpython3.7'

EXT = local_env.SharedLibrary('usdProxyShapeTranslator', source_files, LIBPREFIX= '')

if (system.os == 'windows'):
    EXT = [EXT[0]]

Return('EXT')
