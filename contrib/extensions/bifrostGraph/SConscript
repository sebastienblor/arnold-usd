# vim: filetype=python

import utils.system as system
import os
from utils.build_tools import find_files_recursive
from utils.mtoa_build_tools import get_library_extension

Import('ext_env')

local_env = ext_env.Clone()

BUILD_BASE_DIR = local_env['BUILD_BASE_DIR']
ROOT_DIR = local_env['ROOT_DIR']

mayaRoot = local_env['MAYA_ROOT']
if system.os == 'darwin':
    mayaRoot = mayaRoot.replace('Maya.app/Contents', '')
    mayaRoot = mayaRoot.replace('//', '/')

if system.os !='windows':
    local_env.Append(CCFLAGS = Split('-fvisibility=hidden -std=c++0x')) # hide symbols by default

# TEMP: add paths to Bifrost SDK temporarily so we can get something rendered
local_env.Append(CPPPATH = [ os.path.join(local_env['BIFROST_ROOT'], 'sdk', 'include'),
                             os.path.join(local_env['BIFROST_ROOT'], 'sdk', 'include', 'Amino') ])
if system.os == 'windows':
    local_env.Append(LIBPATH = [ os.path.join(local_env['BIFROST_ROOT'], 'sdk', 'lib') ])
else:
    local_env.Append(LIBPATH = [ os.path.join(local_env['BIFROST_ROOT'], 'lib') ])
local_env.Append(LIBS = Split('bifrostapi_1_0 bifrostprocessing_1_0 bif_0_9 geometry_0_9 bifrost_object_0_9 AminoSDK_4_1 AminoCore_4_1 AminoCppOpSDK_4_1 AminoPrivateRT_4_1 AminoPrivateRT_4_1 AminoPrivateDM_4_1 AminoPrivateBase_4_1 AminoPrivateCppOpABI_4_1 tbb'))

# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(ROOT_DIR, 'contrib', 'extensions', 'bifrostGraph', 'plugin')
source_files  = [os.path.join('plugin', x) for x in find_files_recursive(src_base_dir, ['.c', '.cpp'])]
EXT = local_env.SharedLibrary('bifShapeTranslator', source_files, LIBPREFIX= '')

if (system.os == 'windows'):
    EXT = [EXT[0]]

Return('EXT')
