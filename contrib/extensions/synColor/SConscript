# vim: filetype=python

import system, os
from build_tools import *
import glob
import subprocess

# import build env
Import('ext_env')
plugin_env = ext_env.Clone()

# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(plugin_env['ROOT_DIR'], 'contrib', 'extensions', 'synColor', 'plugin')
source_files  = [os.path.join('plugin', x) for x in find_files_recursive(src_base_dir, ['.c', '.cpp'])]
include_files = [os.path.join('plugin', x) for x in find_files_recursive(src_base_dir, ['.h'])]

plugin_env.Append(CPPPATH = ['.'])

EXT = plugin_env.SharedLibrary('synColorTranslator', source_files, LIBPREFIX='')

if system.os() == 'windows':
   plugin_env['MSVSSCONSCRIPT'] = plugin_env.File('SConscript')


# import build env
Import('env')
shader_env = env.Clone()

# Automatically add all source and include files found in the source path
src_base_dir  = os.path.join(shader_env['ROOT_DIR'], 'contrib', 'extensions', 'synColor', 'shaders')
source_files  = [os.path.join('shaders', x) for x in find_files_recursive(src_base_dir, ['.c', '.cpp'])]
include_files = [os.path.join('shaders', x) for x in find_files_recursive(src_base_dir, ['.h'])]

mayaRoot = shader_env['MAYA_ROOT']
if system.os() == 'darwin':
    mayaRoot = mayaRoot.replace('Maya.app/Contents', '')
    mayaRoot = mayaRoot.replace('//', '/')

shader_env.Append(CPPPATH = ['.'])

# Add the syncolor paths
syncolor_include_path = os.path.join(shader_env['ROOT_DIR'], 'external', 'synColor', 'include')
syncolor_library_path = os.path.join(shader_env['ROOT_DIR'], 'external', 'synColor', 'lib', system.os())
maya_version = shader_env['MAYA_VERSION']

shader_env.Append(CPPPATH = [syncolor_include_path])
shader_env.Append(CPPPATH = [os.path.join(mayaRoot, 'include')])

shader_env.Append(LIBPATH = [syncolor_library_path])

# Add platform specific paths
if system.os() =='windows':
   shader_env.Append(LIBS=Split('ai.lib'))
   shader_env.Append(LIBS=Split('synColor.lib'))
else:
   shader_env.Append(LIBS=Split('ai'))
   shader_env.Append(CCFLAGS = Split('-fvisibility=hidden  -std=c++0x')) # hide symbols by default

   synColorLibs = glob.glob(syncolor_library_path + "/"+ get_library_prefix() + "synColor*"+get_library_extension())

   for syncolorFile in synColorLibs:
      syncolorFile = os.path.basename(syncolorFile)
      extLength = -len(get_library_extension())
      syncolorFile = syncolorFile[3:extLength]
      shader_env.Append(LIBS=Split(syncolorFile))


EXT_SHADERS = shader_env.SharedLibrary('synColor_shaders', source_files, LIBPREFIX='')

def osx_hardcode_path(target, source, env):
   cmd = " install_name_tool -add_rpath @loader_path/../bin/"
   if cmd:
      p = subprocess.Popen(cmd + " " + str(target[0]), shell=True)
      retcode = p.wait()
      return 0

if system.os() == 'windows':
   shader_env['MSVSSCONSCRIPT'] = shader_env.File('SConscript')

if system.os() == 'linux':
   shader_env.Append(RPATH = shader_env.Literal(os.path.join('\\$$ORIGIN', '..', 'bin')))

if system.os() == 'darwin':
   env.AddPostAction(EXT_SHADERS, Action(osx_hardcode_path, 'Adjusting paths in synColor shader...'))

Return('EXT EXT_SHADERS')
