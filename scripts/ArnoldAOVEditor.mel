
proc int ListIndex(string $item, string $list[])
{
   for ($i=0; $i<size($list); $i++)
   {
      if ($list[$i] == $item)
      {
         return $i;
      }
   }
   return -1;
}

global string $g_AnoldStdAOVs[] = {"direct_diffuse",
                                   "direct_specular",
                                   "indirect_diffuse",
                                   "indirect_specular",
                                   "emission",
                                   "reflection",
                                   "refraction",
                                   "sss"};

global proc ArnoldAddAOV(string $node)
{
   string $sel[] = `textScrollList -query -selectItem ArnoldAOVUI_availableLst`;

   for ($aov in $sel)
   {
      int $i, $n = `getAttr -size ($node+".aovs")`;
      for ($i=0; $i<$n; $i++)
      {
         string $name = `getAttr ($node+".aovs["+$i+"].aov_name")`;
         if ($name == "")
         {
            break;
         }
      }

      setAttr -type "string" ($node+".aovs["+$i+"].aov_name") $aov;
      setAttr -type "string" ($node+".aovs["+$i+"].aov_prefix") "";

      textScrollList -edit -append $aov ArnoldAOVUI_activeLst;
      textScrollList -edit -removeItem $aov ArnoldAOVUI_availableLst;
   }
}

global proc ArnoldRemAOV(string $node)
{
   string $sel[] = `textScrollList -query -selectItem ArnoldAOVUI_activeLst`;

   for ($aov in $sel)
   {
      int $i, $n = `getAttr -size ($node+".aovs")`;
      for ($i=0; $i<$n; $i++)
      {
         string $name = `getAttr ($node+".aovs["+$i+"].aov_name")`;
         if ($name == $aov)
         {
            int $j;
            for ($j=$i+1; $j<$n; $j++)
            {
               setAttr -type "string" ($node+".aovs["+($j-1)+"].aov_name") `getAttr ($node+".aovs["+$j+"].aov_name")`;
               setAttr -type "string" ($node+".aovs["+($j-1)+"].aov_prefix") `getAttr ($node+".aovs["+$j+"].aov_prefix")`;
            }

            setAttr -type "string" ($node+".aovs["+($n-1)+"].aov_name") "";
            setAttr -type "string" ($node+".aovs["+($n-1)+"].aov_prefix") "";

            textScrollList -edit -append $aov ArnoldAOVUI_availableLst;
            textScrollList -edit -removeItem $aov ArnoldAOVUI_activeLst;

            break;
         }
      }
   }

   textField -edit -enable false ArnoldAOVUI_prefixFld;
}

global proc ArnoldSetAOVPrefix(string $node)
{
   string $sel[] = `textScrollList -query -selectItem ArnoldAOVUI_activeLst`;

   if (size($sel) != 1)
   {
      return;
   }

   string $aov = $sel[0];

   int $i, $n = `getAttr -size ($node+".aovs")`;
   for ($i=0; $i<$n; $i++)
   {
      string $name = `getAttr ($node+".aovs["+$i+"].aov_name")`;
      if ($name == $aov)
      {
         setAttr -type "string" ($node+".aovs["+$i+"].aov_prefix") `textField -query -text ArnoldAOVUI_prefixFld`;
         break;
      }
   }
}

global proc ArnoldSelectAOV(string $node)
{
   string $sel[] = `textScrollList -query -selectItem ArnoldAOVUI_activeLst`;

   if (size($sel) != 1)
   {
      textField -edit -enable false ArnoldAOVUI_prefixFld;
      return;
   }
   else
   {
      textField -edit -enable true ArnoldAOVUI_prefixFld;
   }

   string $aov = $sel[0];

   int $i, $n = `getAttr -size ($node+".aovs")`;
   for ($i=0; $i<$n; $i++)
   {
      string $name = `getAttr ($node+".aovs["+$i+"].aov_name")`;
      if ($name == $aov)
      {
         string $prefix = `getAttr ($node+".aovs["+$i+"].aov_prefix")`;
         textField -edit -text $prefix ArnoldAOVUI_prefixFld;
         break;
      }
   }
}

global proc ArnoldToggleAOVBatchModeOnly(string $node)
{
   setAttr ($node+".aov_batch_mode_only") `checkBox -query -value ArnoldAOVUI_bmoChk`;
}

global proc ArnoldAOVEditor()
{
   if (`window -exists "ArnoldAOVUI"` == 1)
   {
      deleteUI "ArnoldAOVUI";
   }

   string $win = `window -title "AOV setup" -width 640 -height 300 ArnoldAOVUI`;

   string $aovnode = "";
   string $conns[] = `listConnections -s 1 -d 0 defaultArnoldRenderOptions.aovs`;
   if (size($conns) == 0)
   {
      $aovnode = `createNode ArnoldAOV`;
      connectAttr ($aovnode+".message") defaultArnoldRenderOptions.aovs;
   }
   else
   {
      $aovnode = $conns[0];
   }

   string $aovList[] = {};
   int $caov, $naovs = `getAttr -size ($aovnode+".aovs")`;
   for ($caov=0; $caov<$naovs; $caov++)
   {
      string $aov = `getAttr ($aovnode+".aovs[" + $caov + "].aov_name")`;
      if ($aov != "")
      {
         $aovList[size($aovList)] = $aov;
      }
   }

   $form = `formLayout -numberOfDivisions 100 ArnoldAOVUI_topFrm`;

   text -align "center" -label "Only output AOVs in batch mode" -parent $form ArnoldAOVUI_bmoLbl;
   checkBox -label "" -value `getAttr ($aovnode+".aov_batch_mode_only")` -parent $form -changeCommand ("ArnoldToggleAOVBatchModeOnly(\""+$aovnode+"\")") ArnoldAOVUI_bmoChk;
   text -align "center" -label "Available AOVs" -parent $form ArnoldAOVUI_availableLbl;
   text -align "center" -label "Active AOVs" -parent $form ArnoldAOVUI_activeLbl;
   textScrollList -numberOfRows 10 -allowMultiSelection true -parent $form -doubleClickCommand ("ArnoldAddAOV(\""+$aovnode+"\")") ArnoldAOVUI_availableLst;
   textScrollList -numberOfRows 10 -allowMultiSelection true -parent $form -doubleClickCommand ("ArnoldRemAOV(\""+$aovnode+"\")") -selectCommand ("ArnoldSelectAOV(\""+$aovnode+"\")") ArnoldAOVUI_activeLst;
   button -label ">>" -parent $form -command ("ArnoldAddAOV(\""+$aovnode+"\")") ArnoldAOVUI_addBtn;
   button -label "<<" -parent $form -command ("ArnoldRemAOV(\""+$aovnode+"\")") ArnoldAOVUI_remBtn;
   text -align "center" -label "Prefix" -parent $form ArnoldAOVUI_prefixLbl;
   textField -enable false -text "" -parent $form -changeCommand ("ArnoldSetAOVPrefix(\""+$aovnode+"\")") ArnoldAOVUI_prefixFld;

   global string $g_AnoldStdAOVs[];

   for ($i=0; $i<size($g_AnoldStdAOVs); $i++)
   {
      if (ListIndex($g_AnoldStdAOVs[$i], $aovList) == -1)
      {
         textScrollList -edit -append $g_AnoldStdAOVs[$i] ArnoldAOVUI_availableLst;
      }
      else
      {
         textScrollList -edit -append $g_AnoldStdAOVs[$i] ArnoldAOVUI_activeLst;
      }
   }

   formLayout -edit
              
              -attachForm     ArnoldAOVUI_bmoLbl "top" 2
              -attachPosition ArnoldAOVUI_bmoLbl "right" 2 50
              -attachForm     ArnoldAOVUI_bmoLbl "left" 2
              -attachNone     ArnoldAOVUI_bmoLbl "bottom"
              
              -attachForm     ArnoldAOVUI_bmoChk "top" 2
              -attachPosition ArnoldAOVUI_bmoChk "left" 2 50
              -attachForm     ArnoldAOVUI_bmoChk "right" 2
              -attachNone     ArnoldAOVUI_bmoChk "bottom"
              
              -attachControl  ArnoldAOVUI_availableLbl "top" 2 ArnoldAOVUI_bmoLbl
              -attachForm     ArnoldAOVUI_availableLbl "left" 2
              -attachPosition ArnoldAOVUI_availableLbl "right" 10 50
              -attachNone     ArnoldAOVUI_availableLbl "bottom"
              
              -attachControl  ArnoldAOVUI_activeLbl "top" 2 ArnoldAOVUI_bmoChk
              -attachForm     ArnoldAOVUI_activeLbl "right" 2
              -attachPosition ArnoldAOVUI_activeLbl "left" 10 50
              -attachNone     ArnoldAOVUI_activeLbl "bottom"
              
              -attachControl  ArnoldAOVUI_availableLst "top" 2 ArnoldAOVUI_availableLbl
              -attachForm     ArnoldAOVUI_availableLst "left" 2 
              -attachControl  ArnoldAOVUI_availableLst "right" 2 ArnoldAOVUI_addBtn
              -attachControl  ArnoldAOVUI_availableLst "bottom" 2 ArnoldAOVUI_prefixFld
              
              -attachControl  ArnoldAOVUI_activeLst "top" 2 ArnoldAOVUI_activeLbl
              -attachForm     ArnoldAOVUI_activeLst "right" 2 
              -attachControl  ArnoldAOVUI_activeLst "left" 2 ArnoldAOVUI_addBtn
              -attachControl  ArnoldAOVUI_activeLst "bottom" 2 ArnoldAOVUI_prefixFld
              
              -attachNone     ArnoldAOVUI_addBtn "top"
              -attachPosition ArnoldAOVUI_addBtn "bottom" 2 50
              -attachPosition ArnoldAOVUI_addBtn "left" 2 45
              -attachPosition ArnoldAOVUI_addBtn "right" 2 55
              
              -attachPosition ArnoldAOVUI_remBtn "top" 2 50
              -attachPosition ArnoldAOVUI_remBtn "left" 2 45
              -attachPosition ArnoldAOVUI_remBtn "right" 2 55
              -attachNone     ArnoldAOVUI_remBtn "bottom"
              
              -attachNone     ArnoldAOVUI_prefixLbl "top"
              -attachForm     ArnoldAOVUI_prefixLbl "left" 2
              -attachPosition ArnoldAOVUI_prefixLbl "right" 2 25
              -attachForm     ArnoldAOVUI_prefixLbl "bottom" 2
              
              -attachNone     ArnoldAOVUI_prefixFld "top"
              -attachPosition ArnoldAOVUI_prefixFld "left" 2 25
              -attachForm     ArnoldAOVUI_prefixFld "right" 2
              -attachForm     ArnoldAOVUI_prefixFld "bottom" 2
              $form;

   showWindow $win;
}
