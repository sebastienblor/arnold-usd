
global proc UpdateOutputSettings()
{
   int $flag = `getAttr defaultArnoldRenderOptions.threads_autodetect` == false;
   attrControlGrp -edit -enable $flag os_threads;
}

global proc UpdateSamplingSettings()
{
   int $flag = `getAttr defaultArnoldRenderOptions.use_sample_clamp` == true;
   attrControlGrp -edit -enable $flag ss_max_value;
}

global proc UpdateMotionBlurSettings()
{
   int $flag = `getAttr defaultArnoldRenderOptions.motion_blur_enable` == true;
   attrControlGrp -edit -enable $flag mb_camera_enable;
   attrControlGrp -edit -enable $flag mb_objects_enable;
   attrControlGrp -edit -enable $flag mb_lights_enable;
   attrControlGrp -edit -enable $flag mb_shutter_start;
   attrControlGrp -edit -enable $flag mb_shutter_end;
   attrControlGrp -edit -enable $flag mb_shutter_type;
   attrControlGrp -edit -enable $flag mb_motion_steps;
}

global proc SelectBackground()
{
   int $bkg = `getAttr defaultArnoldRenderOptions.background`;

   switch ($bkg)
   {
      case 0:
      break;
      
      case 1: // Image
      {
         createNode -shared -n "defaultBackgroundImageShader" ArnoldBackgroundImageShader;
      }
      break;
      
      case 2: // Sky
      {
         createNode -shared -n "defaultSkyShader" ArnoldSkyShader;
      }
      break;
      
      case 3: // Sky HDRI
      {
         createNode -shared -n "defaultSky_HDRIShader" ArnoldSky_HDRIShader;
      }
      break;
   }
}

global proc SelectAtmosphere()
{
   int $bkg = `getAttr defaultArnoldRenderOptions.atmosphere`;

   switch ($bkg)
   {
      case 0:
      break;
      
      case 1: // Fog
      {
         createNode -shared -n "defaultFogShader" ArnoldFogShader;
      }
      break;
      
      case 2: // Volume Scattering
      {
         createNode -shared -n "defaultVolumeScatteringShader" ArnoldVolumeScatteringShader;
      }
      break;
   }
}

global proc CreateArnoldOutputSettings()
{
   setUITemplate -pushTemplate attributeEditorTemplate;
   columnLayout -adjustableColumn true arnoldOutputLayout;
      
      attrControlGrp -label "Autodetect Threads"
         -attribute defaultArnoldRenderOptions.threads_autodetect
         -cc UpdateOutputSettings
         os_threads_autodetect;
 
      attrControlGrp -label "Threads"
         -attribute defaultArnoldRenderOptions.threads
         os_threads;
 
      separator;
                 
      attrControlGrp -label "Bucket Scanning"
         -attribute defaultArnoldRenderOptions.bucket_scanning
         os_bucket_scanning;
            
      attrControlGrp -label "Bucket Size"
         -attribute defaultArnoldRenderOptions.bucket_size
         os_bucket_size;
            
      attrControlGrp -label "Clear Before Render"
         -attribute defaultArnoldRenderOptions.clear_before_render
         os_clear_before_render;
         
      attrControlGrp -label "Abort On Error"
         -attribute defaultArnoldRenderOptions.abort_on_error
         os_abort_on_error;
                        
      setParent ..;

   setUITemplate -popTemplate;
}

global proc CreateArnoldSamplingSettings()
{
   setUITemplate -pushTemplate attributeEditorTemplate;
   columnLayout -adjustableColumn true;
      
      attrControlGrp -label "AA Samples"
         -attribute defaultArnoldRenderOptions.AA_samples
         ss_AA_samples;

      attrControlGrp -label "Hemi Samples"
         -attribute defaultArnoldRenderOptions.GI_hemi_samples
         ss_hemi_samples;

      attrControlGrp -label "Glossy Samples"
         -attribute defaultArnoldRenderOptions.GI_specular_samples
         ss_glossy_samples;

      attrControlGrp -label "Clamp Sample Values"
         -attribute defaultArnoldRenderOptions.use_sample_clamp
         -cc UpdateSamplingSettings
         ss_clamp_sample_values;

      attrControlGrp -label "Max. Value"
         -attribute defaultArnoldRenderOptions.AA_sample_clamp
         ss_max_value;

      setParent ..;

   setUITemplate -popTemplate;
}

global proc CreateArnoldGammaSettings()
{
   setUITemplate -pushTemplate attributeEditorTemplate;
   columnLayout -adjustableColumn true;
      
      attrControlGrp -label "Driver gamma"
         -attribute defaultArnoldRenderOptions.driver_gamma
         ss_driver_gamma;

      separator;
      
      attrControlGrp -label "Lights"
         -attribute defaultArnoldRenderOptions.TM_lgamma
         ss_light_gamma;

      attrControlGrp -label "Shaders"
         -attribute defaultArnoldRenderOptions.TM_sgamma
         ss_shader_gamma;

      attrControlGrp -label "Textures"
         -attribute defaultArnoldRenderOptions.TM_tgamma
         ss_texture_gamma;

      setParent ..;

   setUITemplate -popTemplate;
}

global proc CreateArnoldRayDepthSettings()
{
   setUITemplate -pushTemplate attributeEditorTemplate;
   columnLayout -adjustableColumn true;
      
      attrControlGrp -label "Total depth"
         -attribute defaultArnoldRenderOptions.GI_total_depth
         rs_total_depth;

      separator -style "none";
      
      attrControlGrp -label "Diffuse depth"
         -attribute defaultArnoldRenderOptions.GI_diffuse_depth
         rs_diffuse_depth;

      attrControlGrp -label "Glossy depth"
         -attribute defaultArnoldRenderOptions.GI_glossy_depth
         rs_glossy_depth;

      attrControlGrp -label "Reflection depth"
         -attribute defaultArnoldRenderOptions.GI_reflection_depth
         rs_reflection_depth;

      attrControlGrp -label "Refraction depth"
         -attribute defaultArnoldRenderOptions.GI_refraction_depth
         rs_refraction_depth;

      setParent ..;

   setUITemplate -popTemplate;
}

global proc CreateArnoldEnvironmentSettings()
{
   setUITemplate -pushTemplate attributeEditorTemplate;
   columnLayout -adjustableColumn true;
      
      attrControlGrp -label "Background"
         -attribute defaultArnoldRenderOptions.background
         -cc SelectBackground
         es_background;

      button -label "Edit Background" -c SelectBackground;

      separator -style "none";
            
      attrControlGrp -label "Atmosphere"
         -attribute defaultArnoldRenderOptions.atmosphere
         -cc SelectAtmosphere
         es_atmosphere;

      button -label "Edit Atmosphere" -c SelectAtmosphere;

      setParent ..;

   setUITemplate -popTemplate;
}

global proc CreateArnoldMotionBlurSettings()
{
   setUITemplate -pushTemplate attributeEditorTemplate;
   columnLayout -adjustableColumn true;
      
      attrControlGrp -label "Enable"
         -attribute defaultArnoldRenderOptions.motion_blur_enable
         -cc UpdateMotionBlurSettings
         mb_enable;

      attrControlGrp -label "Enable MB for camera"
         -attribute defaultArnoldRenderOptions.mb_camera_enable
         mb_camera_enable;

      attrControlGrp -label "Enable MB for objects"
         -attribute defaultArnoldRenderOptions.mb_objects_enable
         mb_objects_enable;

      attrControlGrp -label "Enable MB for lights"
         -attribute defaultArnoldRenderOptions.mb_lights_enable
         mb_lights_enable;

      separator;
      
      attrControlGrp -label "Shutter Start"
         -attribute defaultArnoldRenderOptions.shutter_start
         mb_shutter_start;

      attrControlGrp -label "Shutter End"
         -attribute defaultArnoldRenderOptions.shutter_end
         mb_shutter_end;

      attrControlGrp -label "Shutter Type"
         -attribute defaultArnoldRenderOptions.shutter_type
         mb_shutter_type;

      attrControlGrp -label "Motion steps"
         -attribute defaultArnoldRenderOptions.motion_steps
         mb_motion_steps;

      setParent ..;

   setUITemplate -popTemplate;
}

global proc CreateArnoldRendererGlobalsTab()
{
   // Make sure the ArnoldRenderOptions node exists
   createNode -skipSelect -shared -n "defaultArnoldRenderOptions" ArnoldRenderOptions;
   
   string $parentForm = `setParent -query`;

   setUITemplate -pushTemplate attributeEditorTemplate;
   scrollLayout -horizontalScrollBarThickness 0 arnoldGlobalsScrollLayout;
      columnLayout -adjustableColumn true arnoldTabColumn;
      
         // Sampling
         //
         frameLayout -label "Sampling" -lw 100 -cll true -cl 0 arnoldSamplingSettings;
            CreateArnoldSamplingSettings;
            setParent ..;
            
         // Ray depth
         //
         frameLayout -label "Ray Depth" -lw 100 -cll true -cl 0 arnoldRayDepthSettings;
            CreateArnoldRayDepthSettings;
            setParent ..;
         
         // Environment
         //
         frameLayout -label "Environment" -lw 150 -cll true -cl 1 arnoldEnvironmentSettings;
            CreateArnoldEnvironmentSettings;
            setParent ..;
         
         // Motion Blur
         //
         frameLayout -label "Motion Blur" -lw 150 -cll true -cl 1 arnoldMotionBlurSettings;
            CreateArnoldMotionBlurSettings;
            setParent ..;
         
         // Output
         //
         frameLayout -label "System" -lw 100 -cll true -cl 1 arnoldOutputSettings;
            CreateArnoldOutputSettings;
            setParent ..;
         
         // Gamma correction
         //
         frameLayout -label "Gamma Correction" -lw 150 -cll true -cl 1 arnoldGammaSettings;
            CreateArnoldGammaSettings;
            setParent ..;
         
         setParent ..;
         
      formLayout -edit
         -af arnoldGlobalsScrollLayout "top" 0
         -af arnoldGlobalsScrollLayout "bottom" 0
         -af arnoldGlobalsScrollLayout "left" 0
         -af arnoldGlobalsScrollLayout "right" 0
         $parentForm;
     
      setParent $parentForm;
   
   UpdateArnoldRendererGlobalsTab();
}

global proc UpdateArnoldRendererGlobalsTab()
{
   UpdateOutputSettings();
   UpdateSamplingSettings();
   UpdateMotionBlurSettings();
}
