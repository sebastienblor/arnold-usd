source "ArnoldRender.mel";
source "AEArnoldShapeTemplate.mel";

global proc ArnoldAddGlobalsTabs()
{
    renderer -edit -addGlobalsTab "Common" "CreateArnoldRendererCommonGlobalsTab" "UpdateArnoldRendererCommonGlobalsTab" arnold;
    renderer -edit -addGlobalsTab "Arnold Renderer" "CreateArnoldRendererGlobalsTab" "UpdateArnoldRendererGlobalsTab" arnold;
}

global proc ArnoldChangeRendererUpdate()
{
    if(!`pluginInfo -q -l "Arnold Render"`)
        return;

    if (`getApplicationVersionAsFloat` >= 2009)
    {
        if (`window -exists unifiedRenderGlobalsWindow`) {
            addRendererTabsToGlobalsWindow("arnold");
            fillSelectedTabForCurrentRenderer();
        }
    }    
}

global proc RegisterArnoldRenderer()
{
   createNode -skipSelect -shared -n "defaultArnoldRenderOptions" ArnoldRenderOptions;

   int $alreadyRegistered = `renderer -exists arnold`;
   if (!$alreadyRegistered)
   {   
      renderer -rendererUIName "Arnold Renderer"
               -renderProcedure "ArnoldRender"
               -renderRegionProcedure "mayaRenderRegion"
               -commandRenderProcedure "ArnoldBatchRender"
               -batchRenderProcedure "ArnoldBatchRender"
               -iprRenderProcedure "ArnoldIprRender"
               -isRunningIprProcedure "ArnoldIprIsRunning"
               -startIprRenderProcedure "ArnoldIprStart"
               -stopIprRenderProcedure "ArnoldIprStop"
               -refreshIprRenderProcedure "ArnoldIprRefresh"
               -pauseIprRenderProcedure "ArnoldIprPause"
               -changeIprRegionProcedure "ArnoldIprChangeRegion"
               arnold;
                   
      evalDeferred ArnoldAddGlobalsTabs;

      renderer -edit -addGlobalsNode "defaultArnoldRenderOptions" arnold;
      
      scriptJob -attributeChange ("defaultRenderGlobals.currentRenderer") "ArnoldChangeRendererUpdate";
      scriptJob -event ("SceneOpened") "ArnoldChangeRendererUpdate";

      // Add option in 'Render' menu to export to an .ass file 
      RenRenderMenu mainRenderMenu;
      menuItem -parent mainRenderMenu -divider true;
      menuItem -parent mainRenderMenu -label "Export to Ass..." -c "ArnoldExportAss" exportToAssMenuItem;
   }
}

