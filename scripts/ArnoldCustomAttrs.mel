
global string $ArnoldMeshAttributes[] =
{
//   Attribute                   Label                   Def         Min   Max            SMin  Smax        Change callback

   // Mesh
   //
   "self_shadows",            "Self Shadows",            "1",        "0",  "1",           "0",  "1",        "",
   "opaque",                  "Opaque",                  "1",        "0",  "1",           "0",  "1",        "",
   "diffuse_visibility",      "Visible In Diffuse",      "1",        "0",  "1",           "0",  "1",        "",
   "glossy_visibility",       "Visible In Glossy",       "1",        "0",  "1",           "0",  "1",        "",
   "subdiv_type",             "Subdivision Surface",     "0",        "0",  "1",           "0",  "1",        "SubdivChange",
   "subdiv_iterations",       "Iterations",              "1",        "0",  "100",         "0",  "10",       "",
   "subdiv_pixel_error",      "Pixel Error",             "0",        "0",  "50",          "0",  "10",       "",
   "sss_use_gi",              "Use GI for SSS",          "1",        "0",  "1",           "0",  "1",        "",
   "sss_max_samples",         "Max. Samples for SSS",    "100000",   "0",  "1000000000",  "0",  "1000000",  "",
   "sss_sample_spacing",      "SSS Sample Spacing",      "0.1",      "0",  "10",          "0",  "1",        "",

   // Lights
   //
   "normalize",               "Normalize",               "1",        "0",  "1",           "0",  "1",        "",
   "bounce_factor",           "Bounce Factor",           "1",        "0",  "20",          "0",  "20",       "",
   "bounces",                 "Bounces",                 "999",      "0",  "10000",       "0",  "999",      "",
   "sss_samples",             "SSS Samples",             "-1",       "0",  "100",         "-1", "100",      "",
   "affect_volumetrics",      "Affects Volumetrics",     "1",        "0",  "1",           "0",  "1",        "VolumetricsChange",
   "cast_volumetric_shadows", "Casts Volumetric Shadows","1",        "0",  "1",           "0",  "1",        "",
   "aspect_ratio",            "Aspect Ratio",            "1",        "0",  "100",         "0",  "1",        "",
   "lens_radius",             "Lens Radius",             "0",        "0",  "100",         "0",  "1",        "",
   "solid_angle",             "Solid Angle Sampling",    "0",        "0",  "1",           "0",  "1",        "",
   
// Enums

//   Attribute                   Label                   Def   Enum values                             Change callback

   "subdiv_adaptive_metric",  "Adaptive Metric",         "0",  "$SubdivMetricValues",  "0", "0",  "0", "",
   "sidedness",               "Sidedness",               "1",  "$SidednessValues",     "0", "0",  "0", "",
   
// Object lists

//   Attribute                   Label                   Def         Enum values                             Change callback

   "subdiv_dicing_camera",    "Dicing Camera",           "Default",  "DicingCameraGetList",  "0", "0",  "0", "",


// Light filters

   "light_filters",           "Light Filters",           "",         "",  "0", "0",  "0", ""
   
};

global string $SubdivMetricValues[] = { "Automatic", "Edge Length", "Flatness" };
global string $SidednessValues[] = { "Double", "Single" };

global proc SubdivChange()
{
   int $flag = `checkBoxGrp -query -value1 mesh_subdiv_type` == true;
   control -edit -enable $flag mesh_subdiv_iterations;
   control -edit -enable $flag mesh_subdiv_adaptive_metric;
   control -edit -enable $flag mesh_subdiv_pixel_error;
   control -edit -enable $flag mesh_subdiv_dicing_camera;
}

global proc VolumetricsChange()
{
   int $flag = `checkBoxGrp -query -value1 light_affect_volumetrics` == true;
   control -edit -enable $flag light_cast_volumetric_shadows;
}

global proc DicingCameraGetList()
{
   string $values[] = `ls -type camera`;
   
   menuItem -label "Default";
   for ($J = 0; ($J < size($values)); $J++)
   {
      int $perspective = `camera -query -orthographic $values[$J]` != true;
      if ($perspective)
         menuItem -label $values[$J];
   }
} 

global proc string[] GetCustomAttributeData(string $attrib)
{
   global string $ArnoldMeshAttributes[];
   
   string $data[];
   
   for ($J = 0; ($J < size($ArnoldMeshAttributes)); $J += 8)
   {
      if ($ArnoldMeshAttributes[$J] == $attrib)
      {
         $data[0] = $ArnoldMeshAttributes[$J + 1]; // Label
         $data[1] = $ArnoldMeshAttributes[$J + 2]; // Default value
         $data[2] = $ArnoldMeshAttributes[$J + 3]; // Minimum value
         $data[3] = $ArnoldMeshAttributes[$J + 4]; // Maximum value
         $data[4] = $ArnoldMeshAttributes[$J + 5]; // Soft Minimum value
         $data[5] = $ArnoldMeshAttributes[$J + 6]; // Soft Maximum value
         $data[6] = $ArnoldMeshAttributes[$J + 7]; // Value change callback
         return $data; 
      }
   }

   $data = { "<Unknown Attribute>", "0", "0", "0", "0", "0" };
   return $data;
}

global proc string GetNodeType(string $name)
{
   string $nodeType = `nodeType $name`;
  
   switch($nodeType)
   {
      case "ambientLight":
      case "directionalLight":
      case "pointLight":
      case "spotLight":
      case "areaLight":
         return "light";
   }

   return $nodeType;
}

global proc ArnoldCustomBoolNew(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at bool -dv ((int)$attribData[1]) $nodeName;
   
   checkBoxGrp -label1 $attribData[0] ($nodeType + "_" + $attribName);
   
   if ($attribData[6] != "")
   {
      checkBoxGrp -edit -cc $attribData[6] ($nodeType + "_" + $attribName);
      evalDeferred $attribData[6];
   }
   connectControl -index 2 ($nodeType + "_" + $attribName) $attr;
}

global proc ArnoldCustomBoolReplace(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at bool -dv ((int)$attribData[1]) $nodeName;

   if ($attribData[6] != "")
      evalDeferred $attribData[6];

   connectControl -index 2 ($nodeType + "_" + $attribName) $attr;
}

global proc ArnoldCustomIntNew(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at long -dv ((int)$attribData[1]) -min ((int)$attribData[2]) -max ((int)$attribData[3]) $nodeName;
   
   intSliderGrp -label $attribData[0] -min ((int)$attribData[4]) -max ((int)$attribData[5]) -fmn ((int)$attribData[2]) -fmx ((int)$attribData[3]) ($nodeType + "_" + $attribName);

   if ($attribData[6] != "")
   {
      intSliderGrp -edit -cc $attribData[6] ($nodeType + "_" + $attribName);
      evalDeferred $attribData[6];
   }
   
   connectControl ($nodeType + "_" + $attribName) $attr;
}

global proc ArnoldCustomIntReplace(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at long -dv ((int)$attribData[1]) -min ((int)$attribData[2]) -max ((int)$attribData[3]) $nodeName;
   
   if ($attribData[6] != "")
      evalDeferred $attribData[6];

   connectControl ($nodeType + "_" + $attribName) $attr;
}

global proc ArnoldCustomFloatNew(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at "float" -dv ((float)$attribData[1]) -min ((float)$attribData[2]) -max ((float)$attribData[3]) $nodeName;
   
   floatSliderGrp -label $attribData[0] -min ((float)$attribData[4]) -max ((float)$attribData[5]) -fmn ((float)$attribData[2]) -fmx ((float)$attribData[3]) ($nodeType + "_" + $attribName);
   
   if ($attribData[6] != "")
   {
      floatSliderGrp -edit -cc $attribData[6] ($nodeType + "_" + $attribName);
      evalDeferred $attribData[6];
   }
 
   connectControl ($nodeType + "_" + $attribName) $attr;
}

global proc ArnoldCustomFloatReplace(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at "float" -dv ((float)$attribData[1]) -min ((float)$attribData[2]) -max ((float)$attribData[3]) $nodeName;
   
   if ($attribData[6] != "")
      evalDeferred $attribData[6];

   connectControl ($nodeType + "_" + $attribName) $attr;
}

global proc ArnoldCustomEnumChanged(string $attr, string $userChangeCB)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   int $val = `optionMenuGrp -query -select ($nodeType + "_" + $attribName)`;
   
   setAttr $attr ($val - 1);

   if ($userChangeCB != "")
      evalDeferred($userChangeCB);
}

global proc ArnoldCustomEnumNew(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at "long" -dv ((int)$attribData[1]) $nodeName;
   
   int $val = `getAttr $attr` + 1;

   optionMenuGrp -label $attribData[0] ($nodeType + "_" + $attribName);
   
   // The things that MEL forces you to do...
   string $values[] = eval("string $_dummy[] = " + $attribData[2]); 
   for ($J = 0; ($J < size($values)); $J++)
      menuItem -label $values[$J];

   optionMenuGrp -edit -select $val -cc ("ArnoldCustomEnumChanged \"" + $attr + "\" \"" + $attribData[6] + "\"") ($nodeType + "_" + $attribName);
   
   if ($attribData[6] != "")
      evalDeferred $attribData[6];
}

global proc ArnoldCustomEnumReplace(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
      addAttr -ln $attribName -at "long" -dv ((int)$attribData[1]) $nodeName;

   int $val = `getAttr $attr` + 1;

   optionMenuGrp -edit -select $val -cc ("ArnoldCustomEnumChanged \"" + $attr + "\" \"" + $attribData[6] + "\"") ($nodeType + "_" + $attribName);

   if ($attribData[6] != "")
      evalDeferred $attribData[6];
}

global proc ArnoldCustomObjectListChanged(string $attr, string $userChangeCB)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $val = `optionMenuGrp -query -value ($nodeType + "_" + $attribName)`;
   
   setAttr -type "string" $attr $val;

   if ($userChangeCB != "")
      evalDeferred($userChangeCB);
}

global proc ArnoldCustomObjectListNew(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   
      
   if (!attributeExists($attribName, $nodeName))
   {
      addAttr -ln $attribName -dt "string" $nodeName;
      setAttr -type "string" $attr $attribData[1];
   }
   
   optionMenuGrp -label $attribData[0] ($nodeType + "_" + $attribName);
   
   eval($attribData[2]); 

   string $val = `getAttr $attr`;

   optionMenuGrp -edit -value $val -cc ("ArnoldCustomObjectListChanged \"" + $attr + "\" \"" + $attribData[6] + "\"") ($nodeType + "_" + $attribName);
   
   if ($attribData[6] != "")
      evalDeferred $attribData[6];
}

global proc ArnoldCustomObjectListReplace(string $attr)
{
   string $buffer[];
   tokenize $attr "." $buffer;
   
   string $nodeName   = $buffer[0]; 
   string $attribName = $buffer[1];
   string $nodeType   = GetNodeType($nodeName); 

   string $attribData[] = GetCustomAttributeData($attribName);   

   if (!attributeExists($attribName, $nodeName))
   {
      addAttr -ln $attribName -dt "string" $nodeName;
      setAttr -type "string" $attr $attribData[1];
   }

   deleteUI ($nodeType + "_" + $attribName);
   optionMenuGrp -label $attribData[0] ($nodeType + "_" + $attribName);
   
   eval($attribData[2]); 

   string $val = `getAttr $attr`;

   optionMenuGrp -edit -value $val -cc ("ArnoldCustomObjectListChanged \"" + $attr + "\" \"" + $attribData[6] + "\"") ($nodeType + "_" + $attribName);

   if ($attribData[6] != "")
      evalDeferred $attribData[6];
}
